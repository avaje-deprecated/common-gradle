
/**
 * For deploying as OSS maven project to oss.sonatype.org.
 *
 * REQUIRED in ~/.gradle/gradle.properties:
 *
 * gradle_common= ...
 * sonatypeUsername=...
 * sonatypePassword=...
 *
 * signing.keyId=...
 * signing.password=...
 * signing.secretKeyRingFile=...
 *
 *
 * EXAMPLE USE:
 * -------
 * Specify each of the ext properties below prior to adding.
 *
 * ext {
 *     projectName = 'something'
 *     projectDescription = 'some description'
 *     projectUrl = 'https://github.com/avaje-x/repackage-gradle-plugin'
 *     scmDeveloperConnection = 'scm:git://github.com/avaje-x/repackage-gradle-plugin.git'
 * }
 *
 * apply from: "${gradle_common}/oss.gradle"
 *
 */

//ext {
//    projectName = 'something'
//    projectDescription = 'some description'
//    projectUrl = ''
//    scmDeveloperConnection = ''
//}

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: sonatypeUsername, password: sonatypePassword)
			}

			pom.project {
				name projectName
				//packaging 'jar'
				description projectDescription
				url projectUrl

				scm {
                    url projectUrl
					developerConnection scmDeveloperConnection
				}

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}

				developers {
					developer {
						id 'rbygrave'
						name 'Robin Bygrave'
					}
				}
			}
		}
	}
}